engine marzano(0.1)
language js

pattern handle_one_statement($class_name, $statements, $states_statements, $static_statements, $render_statements) {
    or {
        method_definition($static, $async, $name, $body, $parameters) as $statement where or {
            and {
                $name <: `constructor`,
                $body <: change_this($states_statements)
            },
            and {
                $name <: or { `componentDidUpdate`, `componentDidMount` },
                $body <: change_this($states_statements),
                $statements += `useEffect(() => $body, []);`
            },
            and {
                $name <: `componentWillUnmount`,
                $body <: change_this($states_statements),
                $statements += `useEffect(() => { \n    return () => $body;\n});`
            },
            and {
                $name <: `render`,
                $body <: statement_block(statements = $render_statements)
            },
            and {
                $static <: `static`,
                $body <: change_this($states_statements),
                $static_statements += `$class_name.$name = $parameters => $body;`
            },
            and {
                $async <: `async`,
                $statements += `const $[name]Handler = useCallback(async () => $body, []);`
            },
            and {
                $statement <: after `@computed`,
                $statements += `const $name = useMemo(() => $body, []);`
            },
            and {
                $statements += `const $[name]Handler = useCallback(() => $body, []);`
            }
        },

        public_field_definition($static, $name, $value, $type) as $statement where or {
            and {
                $value <: contains or { `reaction($_, $effect_function)`, `reaction($_, $effect_function, $_)` },
                $effect_function <: or { `($_) => $effect` , `() => $effect` },
                $statements += `useEffect(() => $effect, []);`
            },

            and {
                $value <: object($properties),
                $name <: `defaultProps`,
                $statements += `const props = { \n    $properties,\n    ...inputProps,\n  };`
            },

            and {
                $static <: `static`,
                $static_statements += `$class_name.$name = $value;`
            },
            and {
                $statement <: after `@observable`,
                $capitalized = capitalize(string = $name),
                or {
                    and {
                        $value <: .,
                        $after_value = `undefined`,
                    },
                    $after_value = $value,
                },
                or {
                    and {
                        $type <:  type_annotation(type = $inner_type),
                        $states_statements += `const [$name, set$capitalized] = useState<$inner_type>($after_value);`
                    },
                    and {
                        $states_statements += `const [$name, set$capitalized] = useState($after_value);`
                    }
                }
            },
            and {
                $value <: arrow_function(),
                $statements += `const $[name]Handler = useCallback($value, []);`
            },
            and {
                $statements += `const $name = useRef($value);`
            }
        },
    }
}

pattern change_this($states_statements) {
    maybe contains or {
        assignment_expression(
            left = `this.state`,
            right = object (
                properties = some bubble($states_statements) pair($key, $value) where {
                $capitalized = capitalize(string = $key),
                $states_statements += `const [$key, set$capitalized] = useState($value);`
            }
            )
        ) => .,
        variable_declarator(
            name = object_pattern(properties = some bubble($states_statements) $prop where {
                $capitalized = capitalize(string = $prop),
                $states_statements += `const [$prop, set$capitalized] = useState();`
            }),
            value = `this.state`
        ) => .
    }
}

pattern gather_hooks($hooks) {
    contains or {
        `useEffect` where {
            $hooks <: not some `useEffect`,
            $hooks += `useEffect`
        },
        `useCallback` where {
            $hooks <: not some `useCallback`,
            $hooks += `useCallback`
        },
        `useState` where {
            $hooks <: not some `useState`,
            $hooks += `useState`
        },
        `useRef` where {
            $hooks <: not some `useRef`,
            $hooks += `useRef`
        }
    }
}

pattern adjust_imports() {
    maybe and {
        $hooks = [],
        gather_hooks($hooks),
        $hooks = join(list = $hooks, separator = ", "),
        or {
            // ugly dealing with imports
            contains import_specifier(name = `Component`) => `$hooks`,
            contains `import React from 'react'` as $i where {
                $i <: not contains namespace_import(),
                $i => `import React, { $hooks } from 'react';`
            },
            contains `import React from "react"` as $i where {
                if ($i <: not contains namespace_import()) {
                    $i => `import React, { $hooks } from 'react';`
                } else {
                    $i => `$i\nimport { $hooks } from 'react';`
                }
            }
        }
    }
}

pattern maybe_wrapped_class_declaration($class_name, $body, $class) {
    or {
        export_statement(declaration = class_declaration(name = $class_name, $body, $heritage) as $class),
        class_declaration(name = $class_name, $body, $heritage) as $class
    } where {
        $heritage <: contains extends_clause(value = contains `Component`)
    }
}

pattern first_step() {
    maybe_wrapped_class_declaration($class_name, $body, $class) where {
        $statements = [],
        $states_statements = [],
        $static_statements = [],

        if ($class <: contains extends_clause(type_arguments = contains type_arguments($types))) {
            $type_annotation = `: $types`
        } else {
            $type_annotation = .
        },

        // todo: replace contains with list pattern match once we have the field set
        // we are missing a field for the statements in class_body
        $body <: contains handle_one_statement($class_name, $statements, $states_statements, $static_statements, $render_statements),
        $body <: not contains `componentDidCatch`,
        $class <: not within class_declaration(name = not $class_name),

        if ($body <: contains `static defaultProps = $default_props`) {
            $the_props = "inputProps"
        } else {
            $the_props = "props"
        },


        if ($body <: contains `props`) {
            $args = `$[the_props]$[type_annotation]`
        } else {
            $args = .
        },

        $separator = `\n    `,
        // a bit of hack because we cannot use a code snippet as an argument to a builtin function yet
        $separator += "",
        $states_statements = join(list = $states_statements, $separator),
        $statements = join(list = $statements, $separator),
        $the_function = `($args) => {\n    $states_statements\n\n    $statements\n\n    $render_statements \n}`,

        if ($body <: contains `ViewState`) {
            $the_const = `import { observer } from "mobx-react";\n\nconst $class_name = observer($the_function);`
        } else {
            $the_const = `const $class_name = $the_function;`
        },

        $static_statements = join(list = $static_statements, $separator),
        $class => `$the_const\n$static_statements\n`
    }
}

pattern find_dependencies($hoisted_states, $dependencies) {
    contains bubble($hoisted_states, $dependencies) identifier() as $i where {
        $i <: not `props`,
        $hoisted_states <: some $i,
        $dependencies <: not some $i,
        $dependencies += `$i`
    }
}

pattern rewrite_accesses($hoisted_states) {
    or {
        `this.state.$x` => `$x`,
        `this.$property` as $p where {
            if ($hoisted_states <: some $property) {
                $p => `$property`
            } else {
                $p => `$[property]Handler`
            }
        },

        lexical_declaration(declarations = [variable_declarator(value = or { `this.state`, `this` })]) => .,

        assignment_expression($left, $right) as $assignment where {
            $hoisted_states <: some $left,
            $capitalized = capitalize(string = $left),
            $assignment => `set$[capitalized]($right)`
        },

        `this.setState($x)` as $set_state where {
            $statements = [],
            $x <: contains bubble($statements) pair(key = $key, value = $value) where {
                $capitalized = capitalize(string = $key),
                $statements += `set$capitalized($value);`
            },
            $separator = `\n    `,
            // a bit of hack because we cannot use a code snippet as an argument to a builtin function yet
            $separator += "",
            $statements = join(list = $statements, $separator),
            $set_state => `$statements`
        },

        // to deactivate dependency detection, comment out the following lines
        `$method($f, $dependencies_array)` where {
            $method <: or { `useEffect`, `useCallback`, `useMemo` },
            $dependencies = [],
            $f <: find_dependencies($hoisted_states, $dependencies),
            $dependencies = join(list = $dependencies, separator = ", "),
            $dependencies_array => `[$dependencies]`
        },

        // clean-up props arg -- not needed if only used in constructor, and first step introduced it
        // if it sees it anywhere in the pattern
        arrow_function(parameters=$props, body=$body) where {
            $props <: contains or { `props`, `inputProps` },
            $body <: not contains `props`,
            $props => .
        }
    }
}

pattern gather_accesses($hoisted_states) {
    contains bubble($hoisted_states) variable_declarator($name, $value) where {
        or {
            and {
                $name <: array_pattern(elements = [$used_name, $_]),
                $value <: `useState($_)`
            },
            and {
                $name <: $used_name,
                $value <: or { `useRef($_)`, `useMemo($_, $_)` }
            }
        },
        $hoisted_states += $name
    },

    contains bubble($hoisted_states) or {
        variable_declarator(
            name = array_pattern(elements = [$name, $_]),
            value = `useState($_)`
        ),
        variable_declarator(
            name = $name,
            value = or { `useRef($_)`, `useMemo($_, $_)` }
        )
    } where $hoisted_states += $name
}

pattern second_step() {
    maybe and {
        $hoisted_states = [],
        $hoisted_states += `props`,
        maybe gather_accesses($hoisted_states), // where $_ = log(string = $hoisted_states)
        program(statements =
            some or {
                export_statement(
                    decorator = contains `@observer` => .,
                    declaration = lexical_declaration(declarations = contains rewrite_accesses($hoisted_states))
                ),
                export_statement(
                    declaration = lexical_declaration(declarations = contains rewrite_accesses($hoisted_states))
                ),
                lexical_declaration(declarations = contains rewrite_accesses($hoisted_states))
            }
        )
    }
}

sequential {
    file(body = program(statements = some bubble first_step())),
    file(body = second_step()),
    file(body = second_step()),
    file(body = second_step()),
    //maybe contains bubble `this.$props` => `$props`
    file(body = adjust_imports())
}
